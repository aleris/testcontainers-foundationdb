plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'com.diffplug.spotless' version '6.3.0'
    id 'org.jreleaser' version "1.12.0"
}

group 'earth.adi'
version '1.1.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation("org.testcontainers:testcontainers:1.19.8") {
        exclude group: "org.junit"
    }
    compileOnly("org.projectlombok:lombok:1.18.24")
    annotationProcessor 'org.projectlombok:lombok:1.18.32'

    testImplementation("org.foundationdb:fdb-java:7.1.61")
    testCompileOnly 'org.projectlombok:lombok:1.18.32'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.32'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    testImplementation 'org.slf4j:slf4j-simple:2.0.12'
}

java {
    withJavadocJar()
    withSourcesJar()
}

test {
    environment "DYLD_LIBRARY_PATH", "/usr/local/lib"
    useJUnitPlatform()
}

task updateReadmeVersion(type:Exec) {
    commandLine("sh", "./scripts/updateReadmeVersion.sh")
}

def mavenArtifactDescription = "Testcontainers FoundationDB module."
def stagingDir = layout.buildDirectory.dir("staging-deploy")

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)

            pom {
                name.set("testcontainers-foundationdb")
                description.set(mavenArtifactDescription)
                url.set("https://github.com/aleris/testcontainers-foundationdb")
                licenses {
                    license {
                        name.set("MIT")
                        url.set("https://github.com/aleris/testcontainers-foundationdb/blob/main/LICENSE.md")
                    }
                }
                developers {
                    developer {
                        id.set("aleris")
                        name.set("Adrian Tosca")
                        email.set("adrian.tosca@gmail.com")
                    }
                }
                scm {
                    connection.set("https://github.com/aleris/testcontainers-foundationdb.git")
                    url.set("https://github.com/aleris/testcontainers-foundationdb")
                }
            }
        }
    }
    repositories { maven { url = stagingDir.get().asFile.toURI() } }
}

jreleaser {
    project {
        description.set(mavenArtifactDescription)
        authors.set(["aleris"])
        license.set("MIT")
        inceptionYear = "2023"
    }
    release {
        github {
            repoOwner.set("aleris")
            overwrite = true
        }
    }
    signing {
        active.set(org.jreleaser.model.Active.ALWAYS)
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                register("sonatype") {
                    active.set(org.jreleaser.model.Active.ALWAYS)
                    url.set("https://central.sonatype.com/api/v1/publisher")
                    stagingRepository(stagingDir.get().toString())
                }
            }
        }
    }
}
